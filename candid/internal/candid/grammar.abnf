Prog      = [OWs Def *(";" OWs Def)] [";"] OWs
            [OWs Actor *(";" OWs Actor)] [";"] OWs
Def       = Type / Import
Type      = "type" Sp Id Sp "=" OWs DataType
Import    = "import" Sp Text
Actor     = "service" [Sp Id] OSp ":" Sp [TupType Sp "->" OWs] (ActorType / Id)

ActorType = "{" OWs [MethType *(";" OWs MethType) [";"] OWs] "}"
MethType  = Name OSp ":" OWs (FuncType / Id)
FuncType  = TupType [Sp "->" OWs TupType [Sp FuncAnn]]
FuncAnn   = "oneway" / "query"
TupType   = "(" OWs [ArgType *("," Sp ArgType) ["," OWs]] OWs ")" / ArgType
ArgType   = [Name OSp ":" Sp] DataType
FieldType = [(Nat / Name) OSp ":" OWs] DataType / Nat / Name
DataType  = ConsType / RefType / PrimType / Id

PrimType    = NumType
            / "bool" / "text" / "null" / "reserved" / "empty"
NumType     = "nat8" / "nat16" / "nat32" / "nat64" / "nat"
            / "int8" / "int16" / "int32" / "int64" / "int"
            / "float32" / "float64"

ConsType    = Blob
            / Opt
            / Vec
            / Record
            / Variant

Blob      = "blob"
Opt       = "opt" Ws DataType
Vec       = "vec" Ws DataType
Record    = "record" OSp "{" OWs [Fields] OWs "}"
Variant   = "variant" OSp "{" OWs [Fields] OWs "}"
Fields    = FieldType *(";" OWs FieldType) [";"]

RefType   = Func / Service / Principal
Func      = "func" OSp FuncType
Service   = "service" Sp ActorType
Principal = "principal"

Name  = Id / Text
Id    = (letter / "_") *(letter / digit / "_")
Text  = %x22 *Char %x22 ; "

Char    = Utf
        / ESC 2*2hex
        / ESC escape
        / "\u{" HexNum "}"
Num     = digit *(["_"] digit)
HexNum  = hex *(["_"] hex)
Nat     = "0x" HexNum / Num

Utf       = ascii / UtfEnc
UtfEnc    = %xC2-DF utfcont
          / %xE0 %xA0-BF utfcont
          / %xED %x80-9F utfcont
          / %xE1-EC 2*2utfcont
          / %xEE-EF 2*2utfcont
          / %xF0 %x90-BF 2*2utfcont
          / %xF4 %x80-8F 2*2utfcont
          / %xF1-F3 3*3utfcont
utfcont   = %x80-BF

ascii    = %x20-21 / %x23-5B / %x5D-7E
escape   = "n" / "r" / "t"
          / ESC
          / %x22 ; "
          / %x27 ; '

letter  = %x41-5A / %x61-7A ; A-Z / a-z
digit   = %x30-39 ; 0-9
hex     = digit / %x41-46 / %x61-66 ; A-F / a-f

CommentText = *(ascii / %x22 / %x27 / %x60) ; " ' `
Comment     = "//" CommentText Nl

Nl   = %x0A / %x0D / (%x0D %x0A)
OWs   = *(Sp / %x09 / Comment / Nl)
Ws   = 1*(Sp / %x09 / Comment / Nl)
OSp  = *" "
Sp   = 1*" "
ESC  = %x5C ; \
